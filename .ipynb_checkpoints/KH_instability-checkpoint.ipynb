{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0539df33",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Pkg\n",
    "pkg\"add Oceananigans, CairoMakie\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "459eb9ed",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Oceananigans\n",
    "\n",
    "grid = RectilinearGrid(size=(64, 64), x=(-5, 5), z=(-5, 5),\n",
    "                       topology=(Periodic, Flat, Bounded))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1b8af4e8",
   "metadata": {},
   "outputs": [],
   "source": [
    "shear_flow(x, z, t) = tanh(z)\n",
    "\n",
    "stratification(x, z, t, p) = p.h * p.Ri * tanh(z / p.h)\n",
    "\n",
    "U = BackgroundField(shear_flow)\n",
    "\n",
    "B = BackgroundField(stratification, parameters=(Ri=0.1, h=1/4))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "732ae606",
   "metadata": {},
   "outputs": [],
   "source": [
    "using CairoMakie\n",
    "\n",
    "zF = znodes(grid, Face())\n",
    "zC = znodes(grid, Center())\n",
    "\n",
    "Ri, h = B.parameters\n",
    "\n",
    "fig = Figure(size = (850, 450))\n",
    "\n",
    "ax = Axis(fig[1, 1], xlabel = \"U(z)\", ylabel = \"z\")\n",
    "lines!(ax, shear_flow.(0, zC, 0), zC; linewidth = 3)\n",
    "\n",
    "ax = Axis(fig[1, 2], xlabel = \"B(z)\")\n",
    "lines!(ax, [stratification(0, z, 0, (Ri=Ri, h=h)) for z in zC], zC; linewidth = 3, color = :red)\n",
    "\n",
    "ax = Axis(fig[1, 3], xlabel = \"Ri(z)\")\n",
    "lines!(ax, [Ri * sech(z / h)^2 / sech(z)^2 for z in zF], zF; linewidth = 3, color = :black) # Ri(z)= ∂_z B / (∂_z U)²; derivatives computed by hand\n",
    "\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "54a4d385",
   "metadata": {},
   "outputs": [],
   "source": [
    "model = NonhydrostaticModel(; grid,\n",
    "                            advection = WENO(),\n",
    "                            background_fields = (u=U, b=B),\n",
    "                            closure = ScalarDiffusivity(ν=2e-4, κ=2e-4),\n",
    "                            buoyancy = BuoyancyTracer(),\n",
    "                            tracers = :b)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4aeae8d8",
   "metadata": {},
   "outputs": [],
   "source": [
    "simulation = Simulation(model, Δt=0.01, stop_iteration=1500, verbose=false)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0084c156",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    grow_instability!(simulation, energy)\n",
    "\n",
    "Grow an instability by running `simulation`.\n",
    "\n",
    "Estimates the growth rate ``σ`` of the instability\n",
    "using the fractional change in volume-mean kinetic energy,\n",
    "over the course of the `simulation`\n",
    "\n",
    "``\n",
    "energy(t₀ + Δτ) / energy(t₀) ≈ exp(2 σ Δτ)\n",
    "``\n",
    "\n",
    "where ``t₀`` is the starting time of the simulation and ``t₀ + Δτ``\n",
    "the ending time of the simulation. We thus find that the growth rate\n",
    "is measured by\n",
    "\n",
    "``\n",
    "σ = log(energy(t₀ + Δτ) / energy(t₀)) / (2 Δτ) .\n",
    "``\n",
    "\"\"\"\n",
    "function grow_instability!(simulation, energy)\n",
    "    # Initialize\n",
    "    simulation.model.clock.iteration = 0\n",
    "    t₀ = simulation.model.clock.time = 0\n",
    "    compute!(energy)\n",
    "    energy₀ = energy[1, 1, 1]\n",
    "\n",
    "    # Grow\n",
    "    run!(simulation)\n",
    "\n",
    "    # Analyze\n",
    "    compute!(energy)\n",
    "    energy₁ = energy[1, 1, 1]\n",
    "    Δτ = simulation.model.clock.time - t₀\n",
    "\n",
    "    # ½(u² + v²) ~ exp(2 σ Δτ)\n",
    "    σ = growth_rate = log(energy₁ / energy₀) / 2Δτ\n",
    "\n",
    "    return growth_rate\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4a7839af",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    rescale!(model, energy; target_kinetic_energy = 1e-3)\n",
    "\n",
    "Rescales all model fields so that `energy = target_kinetic_energy`.\n",
    "\"\"\"\n",
    "function rescale!(model, energy; target_kinetic_energy = 1e-6)\n",
    "    compute!(energy)\n",
    "    rescale_factor = √(target_kinetic_energy / energy)\n",
    "\n",
    "    for f in merge(model.velocities, model.tracers)\n",
    "        f .*= rescale_factor\n",
    "    end\n",
    "\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "using Printf"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9e708466",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    convergence(σ)\n",
    "\n",
    "Check if the growth rate has converged. If the array `σ` has at least 2 elements then returns the\n",
    "relative difference between ``σ[end]`` and ``σ[end-1]``.\n",
    "\"\"\"\n",
    "convergence(σ) = length(σ) > 1 ? abs((σ[end] - σ[end-1]) / σ[end]) : 9.1e18 # pretty big (not Inf tho)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "856c15a0",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    estimate_growth_rate(simulation, energy, ω; convergence_criterion=1e-3)\n",
    "\n",
    "Estimates the growth rate iteratively until the relative change\n",
    "in the estimated growth rate ``σ`` falls below `convergence_criterion`.\n",
    "\n",
    "Returns ``σ``.\n",
    "\"\"\"\n",
    "function estimate_growth_rate(simulation, energy, ω, b; convergence_criterion=1e-3)\n",
    "    σ = []\n",
    "    power_method_data = []\n",
    "    compute!(ω)\n",
    "    push!(power_method_data, (ω=collect(interior(ω, :, 1, :)), b=collect(interior(b, :, 1, :)), σ=deepcopy(σ)))\n",
    "\n",
    "    while convergence(σ) > convergence_criterion\n",
    "        compute!(energy)\n",
    "\n",
    "        @info @sprintf(\"About to start power method iteration %d; kinetic energy: %.2e\", length(σ)+1, energy[1, 1, 1])\n",
    "        push!(σ, grow_instability!(simulation, energy))\n",
    "        compute!(energy)\n",
    "\n",
    "        @info @sprintf(\"Power method iteration %d, kinetic energy: %.2e, σⁿ: %.2e, relative Δσ: %.2e\",\n",
    "                       length(σ), energy[1, 1, 1], σ[end], convergence(σ))\n",
    "\n",
    "        compute!(ω)\n",
    "        rescale!(simulation.model, energy)\n",
    "        push!(power_method_data, (ω=collect(interior(ω, :, 1, :)), b=collect(interior(b, :, 1, :)), σ=deepcopy(σ)))\n",
    "    end\n",
    "\n",
    "    return σ, power_method_data\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ce60e36d",
   "metadata": {},
   "outputs": [],
   "source": [
    "u, v, w = model.velocities\n",
    "b = model.tracers.b\n",
    "\n",
    "perturbation_vorticity = Field(∂z(u) - ∂x(w))\n",
    "\n",
    "xω, yω, zω = nodes(perturbation_vorticity)\n",
    "xb, yb, zb = nodes(b)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d367a643",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Random, Statistics\n",
    "\n",
    "mean_perturbation_kinetic_energy = Field(Average(1/2 * (u^2 + w^2)))\n",
    "noise(x, z) = randn()\n",
    "set!(model, u=noise, w=noise, b=noise)\n",
    "rescale!(simulation.model, mean_perturbation_kinetic_energy, target_kinetic_energy=1e-6)\n",
    "growth_rates, power_method_data = estimate_growth_rate(simulation, mean_perturbation_kinetic_energy, perturbation_vorticity, b)\n",
    "\n",
    "@info \"Power iterations converged! Estimated growth rate: $(growth_rates[end])\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1c508504",
   "metadata": {},
   "outputs": [],
   "source": [
    "n = Observable(1)\n",
    "\n",
    "fig = Figure(size=(800, 600))\n",
    "\n",
    "kwargs = (xlabel=\"x\", ylabel=\"z\", limits = ((xω[1], xω[end]), (zω[1], zω[end])), aspect=1,)\n",
    "\n",
    "ω_title(t) = t === nothing ? @sprintf(\"vorticity\") : @sprintf(\"vorticity at t = %.2f\", t)\n",
    "b_title(t) = t === nothing ? @sprintf(\"buoyancy\")  : @sprintf(\"buoyancy at t = %.2f\", t)\n",
    "\n",
    "ax_ω = Axis(fig[2, 1]; title = ω_title(nothing), kwargs...)\n",
    "\n",
    "ax_b = Axis(fig[2, 3]; title = b_title(nothing), kwargs...)\n",
    "\n",
    "ωₙ = @lift power_method_data[$n].ω\n",
    "bₙ = @lift power_method_data[$n].b\n",
    "\n",
    "σₙ = @lift [(i-1, i==1 ? NaN : growth_rates[i-1]) for i in 1:$n]\n",
    "\n",
    "ω_lims = @lift (-maximum(abs, power_method_data[$n].ω) - 1e-16, maximum(abs, power_method_data[$n].ω) + 1e-16)\n",
    "b_lims = @lift (-maximum(abs, power_method_data[$n].b) - 1e-16, maximum(abs, power_method_data[$n].b) + 1e-16)\n",
    "\n",
    "hm_ω = heatmap!(ax_ω, xω, zω, ωₙ; colorrange = ω_lims, colormap = :balance)\n",
    "Colorbar(fig[2, 2], hm_ω)\n",
    "\n",
    "hm_b = heatmap!(ax_b, xb, zb, bₙ; colorrange = b_lims, colormap = :balance)\n",
    "Colorbar(fig[2, 4], hm_b)\n",
    "\n",
    "eigentitle(σ, t) = length(σ) > 0 ? @sprintf(\"Iteration #%i; growth rate %.2e\", length(σ), σ[end]) : @sprintf(\"Initial perturbation fields\")\n",
    "σ_title = @lift eigentitle(power_method_data[$n].σ, nothing)\n",
    "\n",
    "ax_σ = Axis(fig[1, :];\n",
    "            xlabel = \"Power iteration\",\n",
    "            ylabel = \"Growth rate\",\n",
    "            title = σ_title,\n",
    "            xticks = 1:length(power_method_data)-1,\n",
    "            limits = ((0.5, length(power_method_data)-0.5), (-0.25, 0.25)))\n",
    "\n",
    "scatter!(ax_σ, σₙ; color = :blue)\n",
    "\n",
    "frames = 1:length(power_method_data)\n",
    "\n",
    "record(fig, \"powermethod.mp4\", frames, framerate=1) do i\n",
    "       n[] = i\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ff13fdab",
   "metadata": {},
   "outputs": [],
   "source": [
    "total_vorticity = Field(∂z(u) + ∂z(model.background_fields.velocities.u) - ∂x(w))\n",
    "\n",
    "total_b = Field(b + model.background_fields.tracers.b)\n",
    "\n",
    "simulation.output_writers[:vorticity] =\n",
    "    JLD2OutputWriter(model, (ω=perturbation_vorticity, Ω=total_vorticity, b=b, B=total_b, KE=mean_perturbation_kinetic_energy),\n",
    "                     schedule = TimeInterval(0.10 / estimated_growth_rate),\n",
    "                     filename = \"kelvin_helmholtz_instability.jld2\",\n",
    "                     overwrite_existing = true)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dfa8b6a3",
   "metadata": {},
   "outputs": [],
   "source": [
    "@info \"*** Running a simulation of Kelvin-Helmholtz instability...\"\n",
    "run!(simulation)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "74d8566d",
   "metadata": {},
   "outputs": [],
   "source": [
    "@info \"Making a neat movie of stratified shear flow...\"\n",
    "\n",
    "filepath = simulation.output_writers[:vorticity].filepath\n",
    "\n",
    "ω_timeseries = FieldTimeSeries(filepath, \"ω\")\n",
    "b_timeseries = FieldTimeSeries(filepath, \"b\")\n",
    "Ω_timeseries = FieldTimeSeries(filepath, \"Ω\")\n",
    "B_timeseries = FieldTimeSeries(filepath, \"B\")\n",
    "KE_timeseries = FieldTimeSeries(filepath, \"KE\")\n",
    "\n",
    "times = ω_timeseries.times\n",
    "\n",
    "t_final = times[end]\n",
    "\n",
    "n = Observable(1)\n",
    "\n",
    "ωₙ = @lift interior(ω_timeseries, :, 1, :, $n)\n",
    "bₙ = @lift interior(b_timeseries, :, 1, :, $n)\n",
    "\n",
    "fig = Figure(size=(800, 600))\n",
    "\n",
    "kwargs = (xlabel=\"x\", ylabel=\"z\", limits = ((xω[1], xω[end]), (zω[1], zω[end])), aspect=1,)\n",
    "\n",
    "title = @lift @sprintf(\"t = %.2f\", times[$n])\n",
    "\n",
    "ax_ω = Axis(fig[2, 1]; title = \"perturbation vorticity\", kwargs...)\n",
    "\n",
    "ax_b = Axis(fig[2, 3]; title = \"perturbation buoyancy\", kwargs...)\n",
    "\n",
    "ax_KE = Axis(fig[3, :];\n",
    "             yscale = log10,\n",
    "             limits = ((0, t_final), (initial_eigenmode_energy, 1e-1)),\n",
    "             xlabel = \"time\")\n",
    "\n",
    "fig[1, :] = Label(fig, title, fontsize=24, tellwidth=false)\n",
    "\n",
    "ω_lims = @lift (-maximum(abs, interior(ω_timeseries, :, 1, :, $n)) - 1e-16, maximum(abs, interior(ω_timeseries, :, 1, :, $n)) + 1e-16)\n",
    "b_lims = @lift (-maximum(abs, interior(b_timeseries, :, 1, :, $n)) - 1e-16, maximum(abs, interior(b_timeseries, :, 1, :, $n)) + 1e-16)\n",
    "\n",
    "hm_ω = heatmap!(ax_ω, xω, zω, ωₙ; colorrange = ω_lims, colormap = :balance)\n",
    "Colorbar(fig[2, 2], hm_ω)\n",
    "\n",
    "hm_b = heatmap!(ax_b, xb, zb, bₙ; colorrange = b_lims, colormap = :balance)\n",
    "Colorbar(fig[2, 4], hm_b)\n",
    "\n",
    "tₙ = @lift times[1:$n]\n",
    "KEₙ = @lift KE_timeseries[1:$n]\n",
    "\n",
    "lines!(ax_KE, [0, t_final], @. initial_eigenmode_energy * exp(2 * estimated_growth_rate * [0, t_final]);\n",
    "       label = \"~ exp(2 σ t)\",\n",
    "       linewidth = 2,\n",
    "       color = :black)\n",
    "\n",
    "lines!(ax_KE, times, KE_timeseries[:];\n",
    "       label = \"perturbation kinetic energy\",\n",
    "       linewidth = 4, color = :blue, alpha = 0.4)\n",
    "\n",
    "KE_point = @lift Point2f[(times[$n], KE_timeseries[$n][1, 1, 1])]\n",
    "\n",
    "scatter!(ax_KE, KE_point;\n",
    "         marker = :circle, markersize = 16, color = :blue)\n",
    "\n",
    "frames = 1:length(times)\n",
    "\n",
    "record(fig, \"kelvin_helmholtz_instability_perturbations.mp4\", frames, framerate=8) do i\n",
    "    @info \"Plotting frame $i of $(frames[end])...\"\n",
    "    n[] = i\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5b56c0f5",
   "metadata": {},
   "outputs": [],
   "source": [
    "n = Observable(1)\n",
    "\n",
    "Ωₙ = @lift interior(Ω_timeseries, :, 1, :, $n)\n",
    "Bₙ = @lift interior(B_timeseries, :, 1, :, $n)\n",
    "\n",
    "fig = Figure(size=(800, 600))\n",
    "\n",
    "kwargs = (xlabel=\"x\", ylabel=\"z\", limits = ((xω[1], xω[end]), (zω[1], zω[end])), aspect=1,)\n",
    "\n",
    "title = @lift @sprintf(\"t = %.2f\", times[$n])\n",
    "\n",
    "ax_Ω = Axis(fig[2, 1]; title = \"total vorticity\", kwargs...)\n",
    "\n",
    "ax_B = Axis(fig[2, 3]; title = \"total buoyancy\", kwargs...)\n",
    "\n",
    "ax_KE = Axis(fig[3, :];\n",
    "             yscale = log10,\n",
    "             limits = ((0, t_final), (initial_eigenmode_energy, 1e-1)),\n",
    "             xlabel = \"time\")\n",
    "\n",
    "fig[1, :] = Label(fig, title, fontsize=24, tellwidth=false)\n",
    "\n",
    "hm_Ω = heatmap!(ax_Ω, xω, zω, Ωₙ; colorrange = (-1, 1), colormap = :balance)\n",
    "Colorbar(fig[2, 2], hm_Ω)\n",
    "\n",
    "hm_B = heatmap!(ax_B, xb, zb, Bₙ; colorrange = (-0.05, 0.05), colormap = :balance)\n",
    "Colorbar(fig[2, 4], hm_B)\n",
    "\n",
    "tₙ = @lift times[1:$n]\n",
    "KEₙ = @lift KE_timeseries[1, 1, 1, 1:$n]\n",
    "\n",
    "lines!(ax_KE, [0, t_final], @. initial_eigenmode_energy * exp(2 * estimated_growth_rate * [0, t_final]);\n",
    "       label = \"~ exp(2 σ t)\",\n",
    "       linewidth = 2,\n",
    "       color = :black)\n",
    "\n",
    "lines!(ax_KE, times, KE_timeseries[:];\n",
    "       label = \"perturbation kinetic energy\",\n",
    "       linewidth = 4, color = :blue, alpha = 0.4)\n",
    "\n",
    "KE_point = @lift Point2f[(times[$n], KE_timeseries[$n][1, 1, 1])]\n",
    "\n",
    "scatter!(ax_KE, KE_point;\n",
    "         marker = :circle, markersize = 16, color = :blue)\n",
    "\n",
    "axislegend(ax_KE; position = :rb)\n",
    "\n",
    "record(fig, \"kelvin_helmholtz_instability_total.mp4\", frames, framerate=8) do i\n",
    "    n[] = i\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a65bdbfe",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
